import QtQuick
import QtQuick.Effects
import Quickshell
import Quickshell.Io
import Quickshell.Widgets
import Quickshell.Wayland
import Quickshell.Hyprland
import Qt5Compat.GraphicalEffects
import qs.Commons
import "../layouts"
import "."

PanelWindow {
    id: root

    property var pluginApi: null
    property string layoutAlgorithm: ""
    property string lastLayoutAlgorithm: ""

    property bool liveCapture: pluginApi?.manifest?.metadata?.defaultSettings?.liveCapture === true
    property bool moveCursorToActiveWindow: pluginApi?.manifest?.metadata?.defaultSettings?.moveCursorToActiveWindow === true
    property string defaultLayout: pluginApi?.manifest?.metadata?.defaultSettings?.defaultLayout || "masonry"

    property bool isActive: false
    property bool specialActive: false
    property bool animateWindows: false
    property var lastPositions: {}

    anchors {
        top: true
        bottom: true
        left: true
        right: true
    }
    color: "transparent"
    visible: isActive

    WlrLayershell.layer: WlrLayer.Overlay
    WlrLayershell.exclusiveZone: -1
    WlrLayershell.keyboardFocus: isActive ? 1 : 0
    WlrLayershell.namespace: "quickshell:plugin:qs-hyprview"

    IpcHandler {
        target: "plugin:qs-hyprview"
        function toggle() {
            root.layoutAlgorithm = root.defaultLayout;
            root.toggleExpose();
        }
    }

    Connections {
        target: Hyprland
        function onRawEvent(ev) {
            if (!root.isActive && ev.name !== "activespecial")
                return;
            switch (ev.name) {
            case "openwindow":
            case "closewindow":
            case "changefloatingmode":
            case "movewindow":
                Hyprland.refreshToplevels();
                refreshThumbs();
                return;
            case "activespecial":
                var dataStr = String(ev.data);
                var namePart = dataStr.split(",")[0];
                root.specialActive = (namePart.length > 0);
                return;
            default:
                return;
            }
        }
    }

    Timer {
        id: screencopyTimer
        interval: 125
        repeat: true
        running: !root.liveCapture && root.isActive
        onTriggered: root.refreshThumbs()
    }

    function toggleExpose() {
        root.isActive = !root.isActive;
        if (root.isActive) {
            if (root.layoutAlgorithm === 'random') {
                var layouts = ['smartgrid', 'justified', 'bands', 'masonry', 'hero', 'spiral', 'satellite', 'staggered', 'columnar'].filter(l => l !== root.lastLayoutAlgorithm);
                var randomLayout = layouts[Math.floor(Math.random() * layouts.length)];
                root.lastLayoutAlgorithm = randomLayout;
            } else {
                root.lastLayoutAlgorithm = root.layoutAlgorithm;
            }
            exposeArea.currentIndex = 0;
            exposeArea.searchText = "";
            Hyprland.refreshToplevels();
            searchInput.forceActiveFocus();
            refreshThumbs();
        } else {
            root.animateWindows = false;
            root.lastPositions = {};
        }
    }

    function refreshThumbs() {
        if (!root.isActive)
            return;
        for (var i = 0; i < winRepeater.count; ++i) {
            var it = winRepeater.itemAt(i);
            if (it && it.visible && it.refreshThumb) {
                it.refreshThumb();
            }
        }
    }

    FocusScope {
        id: mainScope
        anchors.fill: parent
        focus: true

        Keys.onPressed: event => {
            if (!root.isActive)
                return;
            if (event.key === Qt.Key_Escape) {
                root.toggleExpose();
                event.accepted = true;
                return;
            }

            const total = winRepeater.count;
            if (total <= 0)
                return;
            function moveSelectionHorizontal(delta) {
                var start = exposeArea.currentIndex;
                for (var step = 1; step <= total; ++step) {
                    var candidate = (start + delta * step + total) % total;
                    var it = winRepeater.itemAt(candidate);
                    if (it && it.visible) {
                        exposeArea.currentIndex = candidate;
                        return;
                    }
                }
            }

            function moveSelectionVertical(dir) {
                var startIndex = exposeArea.currentIndex;
                var currentItem = winRepeater.itemAt(startIndex);
                if (!currentItem || !currentItem.visible) {
                    moveSelectionHorizontal(dir > 0 ? 1 : -1);
                    return;
                }
                var curCx = currentItem.x + currentItem.width / 2;
                var curCy = currentItem.y + currentItem.height / 2;
                var bestIndex = -1;
                var bestDy = 99999999;
                var bestDx = 99999999;

                for (var i = 0; i < total; ++i) {
                    var it = winRepeater.itemAt(i);
                    if (!it || !it.visible || i === startIndex)
                        continue;
                    var cx = it.x + it.width / 2;
                    var cy = it.y + it.height / 2;
                    var dy = cy - curCy;
                    if (dir > 0 && dy <= 0)
                        continue;
                    if (dir < 0 && dy >= 0)
                        continue;
                    var absDy = Math.abs(dy);
                    var absDx = Math.abs(cx - curCx);
                    if (absDy < bestDy || (absDy === bestDy && absDx < bestDx)) {
                        bestDy = absDy;
                        bestDx = absDx;
                        bestIndex = i;
                    }
                }
                if (bestIndex >= 0) {
                    exposeArea.currentIndex = bestIndex;
                }
            }

            if (event.key === Qt.Key_Right || event.key === Qt.Key_Tab) {
                moveSelectionHorizontal(1);
                event.accepted = true;
            } else if (event.key === Qt.Key_Left || event.key === Qt.Key_Backtab) {
                moveSelectionHorizontal(-1);
                event.accepted = true;
            } else if (event.key === Qt.Key_Down) {
                moveSelectionVertical(1);
                event.accepted = true;
            } else if (event.key === Qt.Key_Up) {
                moveSelectionVertical(-1);
                event.accepted = true;
            } else if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter) {
                var item = winRepeater.itemAt(exposeArea.currentIndex);
                if (item && item.activateWindow) {
                    item.activateWindow();
                    event.accepted = true;
                }
            }
        }

        MouseArea {
            anchors.fill: parent
            hoverEnabled: false
            z: -1
            onClicked: root.toggleExpose()
        }

        Item {
            id: layoutContainer
            anchors.fill: parent
            anchors.margins: 32

            Column {
                id: layoutRoot
                anchors.fill: parent
                anchors.margins: 48
                spacing: 20

                Item {
                    id: exposeArea
                    width: layoutRoot.width
                    height: layoutRoot.height - searchBox.implicitHeight - layoutRoot.spacing
                    property int currentIndex: 0
                    property string searchText: ""

                    onSearchTextChanged: {
                        currentIndex = (windowLayoutModel.count > 0) ? 0 : -1;
                    }

                    ScriptModel {
                        id: windowLayoutModel
                        property int areaW: exposeArea.width
                        property int areaH: exposeArea.height
                        property string query: exposeArea.searchText
                        property string algo: root.lastLayoutAlgorithm
                        property var rawToplevels: Hyprland.toplevels.values

                        values: {
                            if (areaW <= 0 || areaH <= 0)
                                return [];
                            var q = (query || "").toLowerCase();
                            var windowList = [];
                            var idx = 0;
                            if (!rawToplevels)
                                return [];

                            for (var it of rawToplevels) {
                                var w = it;
                                var clientInfo = w && w.lastIpcObject ? w.lastIpcObject : {};
                                var workspace = clientInfo && clientInfo.workspace ? clientInfo.workspace : null;
                                var workspaceId = workspace && workspace.id !== undefined ? workspace.id : undefined;
                                if (workspaceId === undefined || workspaceId === null)
                                    continue;
                                var size = clientInfo && clientInfo.size ? clientInfo.size : [0, 0];
                                var at = clientInfo && clientInfo.at ? clientInfo.at : [-1000, -1000];
                                if (at[1] + size[1] <= 0)
                                    continue;
                                var title = (w.title || clientInfo.title || "").toLowerCase();
                                var clazz = (clientInfo["class"] || "").toLowerCase();
                                var ic = (clientInfo.initialClass || "").toLowerCase();
                                var app = (w.appId || clientInfo.initialClass || "").toLowerCase();

                                if (q.length > 0) {
                                    var match = title.indexOf(q) !== -1 || clazz.indexOf(q) !== -1 || ic.indexOf(q) !== -1 || app.indexOf(q) !== -1;
                                    if (!match)
                                        continue;
                                }

                                windowList.push({
                                    win: w,
                                    clientInfo: clientInfo,
                                    workspaceId: workspaceId,
                                    width: size[0],
                                    height: size[1],
                                    originalIndex: idx++,
                                    lastIpcObject: w.lastIpcObject
                                });
                            }

                            windowList.sort(function (a, b) {
                                if (a.workspaceId < b.workspaceId)
                                    return -1;
                                if (a.workspaceId > b.workspaceId)
                                    return 1;
                                if (a.originalIndex < b.originalIndex)
                                    return -1;
                                if (a.originalIndex > b.originalIndex)
                                    return 1;
                                return 0;
                            });

                            return LayoutsManager.doLayout(algo, windowList, areaW, areaH);
                        }
                    }

                    Repeater {
                        id: winRepeater
                        model: windowLayoutModel
                        delegate: WindowThumbnail {
                            hWin: modelData.win
                            wHandle: hWin.wayland
                            winKey: String(hWin.address)
                            thumbW: modelData.width
                            thumbH: modelData.height
                            clientInfo: hWin.lastIpcObject
                            targetX: modelData.x
                            targetY: modelData.y
                            hovered: visible && (exposeArea.currentIndex === index)
                            moveCursorToActiveWindow: root.moveCursorToActiveWindow
                        }
                    }
                }

                Rectangle {
                    id: searchBox
                    width: Math.min(layoutRoot.width * 0.6, 480)
                    height: 40
                    radius: 20
                    color: Color.mSurface
                    border.width: 1
                    border.color: Color.mSecondary
                    anchors.horizontalCenter: parent.horizontalCenter

                    TextInput {
                        id: searchInput
                        anchors.fill: parent
                        anchors.leftMargin: 16
                        anchors.rightMargin: 16
                        verticalAlignment: TextInput.AlignVCenter
                        color: Color.mOnSurface
                        font.pixelSize: 16
                        text: exposeArea.searchText
                        activeFocusOnTab: false
                        selectByMouse: true

                        onTextChanged: {
                            exposeArea.searchText = text;
                            root.animateWindows = true;
                        }

                        Text {
                            anchors.fill: parent
                            verticalAlignment: Text.AlignVCenter
                            color: "#88ffffff"
                            font.pixelSize: 14
                            text: "Type to filter windows..."
                            visible: !searchInput.text || searchInput.text.length === 0
                        }
                    }
                }
            }
        }
    }
}
